IF
	Global("ATK_MEL","LOCALS",1)
	!GlobalTimerNotExpired("#localtimer","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("ATK_MEL","LOCALS",0)
		Continue()
END

IF
	Global("FLOATED_MESSAGE","LOCALS",0)
	Detect([PC])
THEN
	RESPONSE #100
		SetGlobal("FLOATED_MESSAGE","LOCALS",1)
		DisplayStringHead(Myself,@8011)  // "This is my last act of service to you, pig. I'll see you in the Abyss!"
		Continue()
END

IF
	!Global("ATK_MEL","LOCALS",0)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),75)
END

IF
	See(NearestEnemyOf(Myself))
	Global("ATK_PHASE","LOCALS",3)
THEN
	RESPONSE #100
		SetGlobal("ATK_PHASE","LOCALS",4)
		SetGlobal("ATK_MEL","LOCALS",2)
		ForceSpell(LastSeenBy(Myself),WIZARD_BURNING_HANDS)  // SPWI103.SPL (Burning Hands)
		Wait(2)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_STUNNED)
	Global("ATK_PHASE","LOCALS",2)
THEN
	RESPONSE #100
		SetGlobal("ATK_PHASE","LOCALS",3)
		ForceSpell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)  // SPWI715.SPL (Power Word, Stun)
		Wait(2)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_STUNNED)
	Global("ATK_PHASE","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("ATK_PHASE","LOCALS",2)
		SetGlobalTimer("#localtimer","LOCALS",17)
		SetGlobal("ATK_MEL","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)  // SPWI715.SPL (Power Word, Stun)
		Wait(2)
END

IF
	See(NearestEnemyOf(Myself))
	Global("ATK_PHASE","LOCALS",0)
	Detect([PC])
THEN
	RESPONSE #100
		SetGlobal("ATK_PHASE","LOCALS",1)
		DisplayStringHead(Myself,@8011)  // "This is my last act of service to you, pig. I'll see you in the Abyss!"
		ForceSpell(Myself,WIZARD_MIRROR_IMAGE)  // SPWI212.SPL (Mirror Image)
		Wait(2)
END

IF
	See([PC])
	!Range(LastSeenBy(Myself),10)
THEN
	RESPONSE #100
		MoveToObject(LastSeenBy(Myself))
END

IF
	!See([PC])
THEN
	RESPONSE #100
		MoveToObject(Player1)
END

